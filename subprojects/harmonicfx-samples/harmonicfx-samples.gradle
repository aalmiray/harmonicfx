/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2013-2020 The original authors
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'java-library'
}

config {
    bintray    { enabled = false }
    publishing { enabled = false }
}

dependencies {
    api project(':harmonicfx-core')
    api "org.kordamp.ikonli:ikonli-fontawesome-pack:$ikonliVersion"
    api "org.kordamp.ikonli:ikonli-material-pack:$ikonliVersion"
    api "org.kordamp.ikonli:ikonli-materialdesign-pack:$ikonliVersion"
    api "org.kordamp.ikonli:ikonli-weathericons-pack:$ikonliVersion"
}

tasks.addRule('Pattern: demo<ControlName>') { String taskName ->
    if (taskName.startsWith('demo')) {
        task (taskName, type: JavaExec) {
            group = 'Demo'
            description = "Run the ${taskName[4..-1]} demo"
            main = "eu.hansolo.fx.${taskName[4..-1].toLowerCase()}.Main".toString()
            classpath sourceSets.main.runtimeClasspath
        }
    }
}
