/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2013-2020 The original authors
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'org.kordamp.gradle.kordamp-parentpom'
}

config {
    info {
        name          = 'HarmonicFX'
        description   = "Hansolo's Friday Fun JavaFX components"
        inceptionYear = '2013'
        tags          = ['widgets', 'javafx']

        specification { enabled = false }

        people {
            person {
                id = 'hansolo_'
                name = 'Gerrit Grunwald'
                roles = ['developer', 'author']
            }
        }
    }

    docs {
        sourceXref {
            inputEncoding = 'UTF-8'
        }

        javadoc {
            options {
                addBooleanOption('javafx', true)
                links('http://docs.oracle.com/javase/8/javafx/api/')
            }
        }
    }
}

allprojects {
    apply plugin: 'idea'
}

idea {
    project {
        jdkName '1.8'
        languageLevel '1.8'

        ipr {
            withXml { provider ->
                def node = provider.asNode()
                node.component.find { it.'@name' == 'VcsDirectoryMappings' }?.mapping[0].'@vcs' = 'Git'
            }
        }
    }
}

subprojects { subproj ->
    license {
        mapping {
            fxml = 'XML_STYLE'
        }
    }
}
